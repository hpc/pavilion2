?expr: or_expr

or_expr: and_expr ( OR and_expr )*
and_expr: not_expr ( AND not_expr )*
not_expr: NOT? (comp_expr | special)
comp_expr: keyword (LT | GT | LT_EQ | GT_EQ | EQ | NOT_EQ) literal

special: COMPLETE
       | ALL_STARTED
       | PASSED
       | FAILED
       | RESULT_ERROR

keyword: NAME
       | STATE
       | USER
       | SYS_NAME
       | HAS_STATE
       | CREATED
       | FINISHED
       | PARTITION
       | NODES
       | NUM_NODES

?literal: time
       | INT
       | GLOB

?time: partial_iso
    | duration

duration: INT [WS] unit ["s"]

partial_iso: partial_iso_date ("T" partial_iso_time)?

partial_iso_date: _iso_year [(("-" _iso_month) [("-" _iso_day)])]

partial_iso_time: _iso_hour [((":" _iso_minutes) [(":" _iso_seconds)])]

_iso_year: /\d{4}/

_iso_month: /\d{2}/

_iso_day: /\d{2}/

_iso_hour: /\d{2}/

_iso_minutes: /\d{2}/

_iso_seconds: NUMBER
    
unit: "second" -> second
    | "minute" -> minute
    | "hour" -> hour
    | "day" -> day
    | "week" -> week
    | "month" -> month
    | "year" -> year

LT: "<"
GT: ">"
LT_EQ: "<="
GT_EQ: ">="
EQ: "="
NOT_EQ: "!="

AND: "AND"i
OR: "OR"i
NOT: "NOT"i

COMPLETE: "COMPLETE"i
ALL_STARTED: "ALL_STARTED"i
PASSED: "PASSED"i
FAILED: "FAILED"i
RESULT_ERROR: "RESULT_ERROR"i

NAME: "NAME"i
STATE: "STATE"i
USER: "USER"i
SYS_NAME: "SYS_NAME"i
HAS_STATE: "HAS_STATE"i
CREATED: "CREATED"i
FINISHED: "FINISHED"i
PARTITION: "PARTITION"i
NODES: "NODES"i
NUM_NODES: "NUM_NODES"i

GLOB: /.+/

%import common.INT
%import common.NUMBER
%import common.WS
