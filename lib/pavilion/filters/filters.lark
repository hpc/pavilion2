expression: WORD
          | WORD str_comparator WORD
          | WORD num_comparator num_value
          | unary_connective expression
          | expression binary_connective expression

num_value: time
     | WORD
     | NUMBER

binary_connective: WS | "|"

unary_connective: "!"

str_comparator: "=="

num_comparator: "<" -> lt
              | ">" -> gt
              | "<=" -> lteq
              | ">=" -> gteq
              | "==" -> eq

time: partial_iso
    | duration

duration: INT [WS] unit ["s"]

partial_iso: partial_iso_date ("T" partial_iso_time)?

partial_iso_date: _iso_year [(("-" _iso_month) [("-" _iso_day)])]

partial_iso_time: _iso_hour [((":" _iso_minutes) [(":" _iso_seconds)])]

_iso_year: /\d{4}/

_iso_month: /\d{2}/

_iso_day: /\d{2}/

_iso_hour: /\d{2}/

_iso_minutes: /\d{2}/

_iso_seconds: /\d{2}/
    
unit: "second" -> second
         | "minute" -> minute
         | "hour" -> hour
         | "day" -> day
         | "month" -> month
         | "year" -> year

%import common.WORD
%import common.INT
%import common.NUMBER
%import common.WS
